plugins {
    id 'java'
}

group = 'inga.jvmdependencyloader'
version = '0.8.0'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes "Main-Class": "inga.jvmdependencyloader.Main"
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(21))
    })
    useJUnitPlatform()
}
